select distinct 
	?book ?isbn 
		?bookTitle 
		?pages 
		?bookReleaseDate 
		?bookReleaseYear
		?bookGenre ?bookGenreTitle 
		?bookCountry ?bookCountryTitle 
		?author ?authorLabel ?authorBirthDate 
			?authorBirthPlace ?authorBirthPlaceLabel
		?movie
			?movieTitle 
{
	?book rdf:type dbo:Work.
	?book dbo:isbn ?isbn.
	OPTIONAL{ ?book rdfs:label ?bookTitle. FILTER(LANGMATCHES(LANG(?bookTitle ), 'en')) }.
	OPTIONAL{ ?book dbo:numberOfPages ?pages }.
	OPTIONAL{ ?book dbo:releaseDate ?bookReleaseDate }.
	OPTIONAL{ ?book dbp:releaseDate ?bookReleaseYear }.
	OPTIONAL{
		?book dbp:genre ?bookGenre.
		OPTIONAL{ ?bookGenre rdfs:label ?bookGenreTitle FILTER(LANGMATCHES(LANG(?bookGenreTitle), 'en')) }.
	}.
	OPTIONAL{
		?book dbp:country ?bookCountry.
		OPTIONAL{?bookCountry rdfs:label ?bookCountryTitle. FILTER(LANGMATCHES(LANG(?bookCountryTitle), 'en')) }.
	}.
	OPTIONAL{
		?book dbo:author ?author . 
		OPTIONAL{  ?author rdfs:label ?authorLabel FILTER(LANGMATCHES(LANG(?authorLabel), 'en')) }.
		OPTIONAL{  ?author dbo:birthDate ?authorBirthDate }.
		OPTIONAL{  
			?author dbo:birthPlace ?authorBirthPlace.
			OPTIONAL{ ?authorBirthPlace rdfs:label ?authorBirthPlaceLabel. FILTER(LANGMATCHES(LANG(?authorBirthPlaceLabel), 'en')) }.
		}.
	}.
	OPTIONAL{
		?movie dbo:basedOn ?book .
		OPTIONAL{ ?movie rdfs:label ?movieTitle. FILTER(LANGMATCHES(LANG(?movieTitle), 'en')) }.
	}.
}
ORDER BY (?movieTitle)
LIMIT 40000 
