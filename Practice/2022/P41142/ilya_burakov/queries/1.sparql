PREFIX chp: <http://iburakov.me/ontologies/chord-progressions#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# - Как будет выглядеть мажорное трезвучие на четвёртой ступени ("IV") в тональности "Ab major"?

SELECT ?abs_chord_repr (COUNT(?abs_chord_repr) AS ?abs_chord_count) WHERE {
	?rel_chord a chp:RelativeChord ;
		chp:hasHookTheoryRepresentation "IV" .
	?progression_entry a chp:ChordProgressionEntry ;
		chp:hasRelativeChord ?rel_chord ;
		chp:hasAbsoluteChord ?abs_chord .
	?composition a chp:Composition ;
		chp:hasChordProgression ?progression_entry ;
		chp:hasKey ?key.
	?key a chp:Key ;
		chp:hasScale [ a chp:Scale ; rdfs:label "major"] ;
		chp:hasTonicNote [ a chp:Note ; rdfs:label "Ab" ] .
	?abs_chord a chp:AbsoluteChord ;
		chp:hasHookTheoryRepresentation ?abs_chord_repr .
}
GROUP BY ?abs_chord_repr
ORDER BY DESC(?abs_chord_count)
