PREFIX chp: <http://iburakov.me/ontologies/chord-progressions#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# - Какие аккордовые последовательности популярны у конкретного жанра?

SELECT ?rel_chord_repr ?next_rel_chord_repr (COUNT(?progression_entry) as $count)  WHERE {
	?progression_entry a chp:ChordProgressionEntry ;
		chp:hasRelativeChord ?rel_chord ;
		chp:hasNextEntry ?next_progression_entry .
	?next_progression_entry a chp:ChordProgressionEntry ;
		chp:hasRelativeChord ?rel_chord_next .
    ?rel_chord a chp:RelativeChord ;
		chp:hasHookTheoryRepresentation ?rel_chord_repr .
    ?rel_chord_next a chp:RelativeChord ;
		chp:hasHookTheoryRepresentation ?next_rel_chord_repr .
	?composition a chp:Composition ;
		chp:hasChordProgression ?progression_entry ;
		chp:hasAuthor ?author;
		chp:hasGenre "jazz".
 	FILTER (?rel_chord_repr != ?next_rel_chord_repr)
}
GROUP BY ?rel_chord_repr ?next_rel_chord_repr
ORDER BY DESC(?count)